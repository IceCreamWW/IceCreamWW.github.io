<hr>
<p>title: “[Awk] Awk Scripts”<br>catalog: true<br>toc_nav_num: true<br>date: 2019-07-15 18:40:06<br>subtitle: “Aggregation of awk techniques and examples”<br>header-img: “Demo.png”<br>top: 0<br>tags:</p>
<ul>
<li>scripts</li>
<li>awk<br>catagories:</li>
<li>scripts</li>
</ul>
<hr>
<blockquote>
<p>本文收录了工作中用到的经典的awk脚本, 并通过这些脚本解释awk的一些语法和技巧</p>
</blockquote>
<h1 id="speech-processing"><a href="#speech-processing" class="headerlink" title="speech processing"></a>speech processing</h1><h2 id="common"><a href="#common" class="headerlink" title="common"></a>common</h2><h3 id="sort-to-field-awk"><a href="#sort-to-field-awk" class="headerlink" title="sort_to_field.awk"></a>sort_to_field.awk</h3><ul>
<li>目标: 将文件1按照文件2的某个键排序</li>
<li>介绍: 排序scp文件的常用操作 </li>
</ul>
<p><strong>AWK</strong></p>
<pre><code class="lang-awk">FNR==NR{
    record[$f] = $0
}
FNR!=NR {
    if ($f in record)
        print record[$f]
}
</code></pre>
<hr>
<p><strong>SHELL + AWK</strong></p>
<pre><code class="lang-bash">cat text.txt refer.txt | awk &#39;{if ($1 in a) print a[$1]; a[$1]=$0}&#39;
</code></pre>
<hr>
<p>示例输入:</p>
<p>text.txt</p>
<pre><code>440c02010 sox -r 16k -e signed -b 16 -c 1 -t raw /path-to-440c0201.wv1 -t wav - |
440c02040 sox -r 16k -e signed -b 16 -c 1 -t raw /path-to-440c0204.wv1 -t wav - |
440c02020 sox -r 16k -e signed -b 16 -c 1 -t raw /path-to-440c0202.wv1 -t wav - |
440c02050 sox -r 16k -e signed -b 16 -c 1 -t raw /path-to-440c0205.wv1 -t wav - |
440c02030 sox -r 16k -e signed -b 16 -c 1 -t raw /path-to-440c0203.wv1 -t wav - |
</code></pre><p>refer.txt</p>
<pre><code class="lang-text">440c02010 440
440c02020 440
440c02030 440
440c02040 440
440c02050 440
</code></pre>
<hr>
<p>示例输出 (sort_to_field.awk -v f=1 text.txt refer.txt):</p>
<pre><code>440c02010 sox -r 16k -e signed -b 16 -c 1 -t raw /path-to-440c0201.wv1 -t wav - |
440c02020 sox -r 16k -e signed -b 16 -c 1 -t raw /path-to-440c0202.wv1 -t wav - |
440c02030 sox -r 16k -e signed -b 16 -c 1 -t raw /path-to-440c0203.wv1 -t wav - |
440c02040 sox -r 16k -e signed -b 16 -c 1 -t raw /path-to-440c0204.wv1 -t wav - |
440c02050 sox -r 16k -e signed -b 16 -c 1 -t raw /path-to-440c0205.wv1 -t wav - |
</code></pre><hr>
<h2 id="VAD"><a href="#VAD" class="headerlink" title="VAD"></a>VAD</h2><h3 id="compare-mlf-awk"><a href="#compare-mlf-awk" class="headerlink" title="compare_mlf.awk"></a>compare_mlf.awk</h3><ul>
<li>目标: 对比两个mlf标注文件中，标注一致和不一致的时间段 (compare_mlf.awk 1.mlf 2.mlf)</li>
<li>介绍: mlf文件是描述音频vad的常用格式, 是一行[uttid]后接着若干行[start end spc/sil]格式的对某一段时间是语音帧/静音帧的判定</li>
</ul>
<hr>
<p><strong>AWK 脚本</strong></p>
<ol>
<li>PROCINFO 控制遍历列表的顺序，取值包括:<ul>
<li>“@unsorted” (默认)</li>
<li>“@ind_num_asc”, “ind_num_desc” (按索引大小排序)</li>
<li>“@val_type_asc” (按值类型排序，数字在字符串前)</li>
<li>“@val_str_asc”</li>
<li>etc.</li>
</ul>
</li>
<li>ARGIND 记录当前文件在ARGV中的索引，从1开始</li>
<li>asort(src, dest, method) 第三个参数从PROCINFO中取值</li>
</ol>
<hr>
<pre><code class="lang-awk">#! /bin/awk -f
#
# -------------------------------------------------------------------------------
# Name:     compare_mlf.awk
# Purpose:  give comparison of two or more standard mlf files
# Author:   Wei Wang


# -------------------------------------------------------------- get_frametype --
# get the type of a frame given the uttid and file id
#
function get_frametype(fileidx, uttid, targetframe,
                       endframe)
{
    # make sure frames are in numeric ascending order in fileidx_uttid_endframe2type[fileidx][uttid]
    PROCINFO[&quot;sorted_in&quot;]=&quot;@ind_num_asc&quot;;
    # forward search endframe to get the interval of target frame
    for (endframe in fileidx_uttid_endframe2type[fileidx][uttid])
        if (endframe+0 &gt;= targetframe+0)
            return fileidx_uttid_endframe2type[fileidx][uttid][endframe]==0 ? &quot;sil&quot;:&quot;spc&quot;;

    # if target frame exceeds all end boundary frames,
    # assume it to be of same type as the last frame interval
    return fileidx_uttid_endframe2type[fileidx][uttid][endframe]==0 ? &quot;sil&quot;:&quot;spc&quot;;
}

# record it when the whole line consists of only one field (utterance id)
NF==1{
    uttid=$1;
}

# $1: start boundary frame number
# $2: end boundary frame number
# $3: type of this frame interval &quot;sil&quot; / &quot;spc&quot;
NF&gt;1{
    # fileidx_uttid_frame2type
        # map a end boundary frame to its type (silence or speech), 3D array
            # dim1: file id (specified by ARGIND)
            # dim2: uttid
            # dim3: end boundary frame number
            # value: 0/1
    fileidx_uttid_endframe2type[ARGIND+0][uttid][$2+0] = ($3==&quot;sil&quot; ? 0:1);

    # record all end boundary frames of a utterance
    endframes[uttid][$2] = $2;
    # record all utterances
    uttids[uttid]=uttid;
    # record all files
    idx2file[ARGIND]=FILENAME;
}
END {
    printf &quot;%-8s%-8s%-9s&quot;, &quot;start&quot;, &quot;end&quot;, &quot;durat&quot;;

    # truncate the first 7 characters to be displayed as file name
    for (idx in idx2file)
        printf &quot;%-8s&quot;, substr(idx2file[idx], 1,7);
    printf &quot;%-8s\n\n&quot;, &quot;judge&quot;;

    # make sure utterance info are output in ascending order (not necessary for correctness)
    PROCINFO[&quot;sorted_in&quot;]=&quot;@ind_str_asc&quot;;
    for (uttid in uttids)
    {
        print uttid;
        startframe=0;

        # make sure endframes are sorted in numeric ascending order (necessary for algorithm correctness)
        PROCINFO[&quot;sorted_in&quot;]=&quot;@ind_num_asc&quot;;
        for(endframe in endframes[uttid])
        {
            if (endframe+0 &lt;= startframe+0)  continue;
            printf &quot;%-8s%-8s%-9s&quot;, startframe, endframe, endframe - startframe;
            judge = &quot;same&quot;; frametype=-1;

            # search for current end frame type in each mlf files, check their consistency
            for (idx in idx2file)
            {
                frametype_new = get_frametype(idx, uttid, endframe);
                printf &quot;%-8s&quot;, frametype_new;
                if (frametype != -1 &amp;&amp; frametype != frametype_new)
                    judge = &quot;diff&quot;;
                frametype = frametype_new;
            }
            printf &quot;%-8s\n&quot;, judge;
            startframe=endframe;
        }
        printf &quot;\n&quot;;
    }
}
</code></pre>
<hr>
<ul>
<li>mlf示例文件</li>
</ul>
<pre><code class="lang-text">440c02010
0       610     sil
610     4680    spc
4680    5730    sil

440c02017
0       610     sil
610     4680    spc
4680    5730    sil
</code></pre>
<pre><code class="lang-text">440c02010
0       620     sil
620     1600    spc
1600    1810    sil
1810    4720    spc
4720    5730    sil

440c02017
0       620     sil
620     1540    spc
1540    1560    sil
1560    1570    spc
1570    1820    sil
1820    4590    spc
4590    4600    sil
4600    4610    spc
4610    5730    sil
</code></pre>
<hr>
<ul>
<li>示例输出 (compare_mlf.awk 1.mlf 2.mlf)</li>
</ul>
<pre><code class="lang-text">start   end     durat    gold.ml pred.lo judge   

440c02010
0       610     610      sil     sil     same    
610     620     10       spc     sil     diff    
620     1600    980      spc     spc     same    
1600    1810    210      spc     sil     diff    
1810    4680    2870     spc     spc     same    
4680    4720    40       sil     spc     diff    
4720    5730    1010     sil     sil     same    

440c02017
0       610     610      sil     sil     same    
610     620     10       spc     sil     diff    
620     1540    920      spc     spc     same    
1540    1560    20       spc     sil     diff    
1560    1570    10       spc     spc     same    
1570    1820    250      spc     sil     diff    
1820    4590    2770     spc     spc     same    
4590    4600    10       spc     sil     diff    
4600    4610    10       spc     spc     same    
4610    4680    70       spc     sil     diff    
4680    5730    1050     sil     sil     same
</code></pre>
